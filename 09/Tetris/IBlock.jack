/** Represents an I block. */

class IBlock {
    static char direction_left, direction_right, direction_up, direction_down, direction_fall;

    field List squares; // individual component squares (length 4)
    field int rotation; // 0-3 (starts at 0) 

    field Array rotation_zero_offsets, rotation_one_offsets, rotation_two_offsets, rotation_three_offsets;
    field Array rotation_offsets_all;

    field int bb_x, bb_y;

    constructor IBlock new(int Ax, int Ay) {
        // Ax: x coordinate (within Tetris field) of top left bounding box square
        // Ay: y coordinadate (within Tetris field) of top left bounding box square

        var Square sq0, sq1, sq2, sq3;

        var String s, t, u, v, w;

        let rotation_zero_offsets = Utils.create_offsets_list(0, 2, 1, 2, 2, 2, 3, 2);
        let rotation_two_offsets = Utils.create_offsets_list(0, 2, 1, 2, 2, 2, 3, 2);

        let rotation_one_offsets = Utils.create_offsets_list(2, 0, 2, 1, 2, 2, 2, 3);
        let rotation_three_offsets = Utils.create_offsets_list(2, 0, 2, 1, 2, 2, 2, 3);

        let rotation_offsets_all = Array.new(4);
        let rotation_offsets_all[0] = rotation_zero_offsets;
        let rotation_offsets_all[1] = rotation_one_offsets;
        let rotation_offsets_all[2] = rotation_two_offsets;
        let rotation_offsets_all[3] = rotation_three_offsets;

        let s = "l";
        let direction_left = s.charAt(0);

        let t = "r";
        let direction_right = t.charAt(0);

        let u = "u";
        let direction_up = u.charAt(0);

        let v = "d";
        let direction_down = v.charAt(0);

        let w = "f";
        let direction_fall = w.charAt(0);
        
        let rotation = 0;
        
        let sq0 = Square.new(Ax, Ay + 2);
        let sq1 = Square.new(Ax + 1, Ay + 2);
        let sq2 = Square.new(Ax + 2, Ay + 2);
        let sq3 = Square.new(Ax + 3, Ay + 2);

        let bb_x = Ax;
        let bb_y = Ay;

        let squares = Array.new(4);
        let squares[0] = sq0;
        let squares[1] = sq1;
        let squares[2] = sq2;
        let squares[3] = sq3;

        do show();
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    method void draw() {
        var int i;
        var Square sq;
        
        let i = 0;
        while (i < 4) {
            let sq = squares[i];
            do sq.draw();
            let i = i + 1;
        }

        return;
    }

    method Array get_coords(){
        var Array coords_all, arr;
        var int i;
        var Square sq;

        let coords_all = Array.new(4);
        let i = 0;
        while (i < 4){
            let sq = squares[i];
            let arr = Array.new(2);
            let arr[0] = sq.get_x();
            let arr[1] = sq.get_y();
            let coords_all[i] = arr;
            let i = i + 1;
        }
        return coords_all;
    }

    method Array generate_post_move_coordinates(char direction){
        var Array new_coords_all;
        var int i, post_move_rotation, bb_x_new, bb_y_new;
        var Array move_offsets, arr, move_offset, bb_offset;

        let bb_offset = Array.new(2);
        let bb_offset[0] = 0;
        let bb_offset[1] = 0;
        
        let post_move_rotation = rotation;

        if (direction = direction_left){
            let bb_offset[0] = -1;
        }
        else {
            if (direction = direction_right){
                let bb_offset[0] = 1;
            }
            else {
                if (direction = direction_fall){
                    let bb_offset[1] = 1;   
                }
                else {
                    if (direction = direction_up){
                        let post_move_rotation = Utils.mod(rotation + 1, 4);
                    }
                    else {
                        if (direction = direction_down) {
                            let post_move_rotation = Utils.mod(rotation - 1, 4);
                        }
                    }
                }
            }
        }

        if (post_move_rotation < 0){
            do Sys.halt();
        }

        let move_offsets = rotation_offsets_all[post_move_rotation];

        let new_coords_all = Array.new(4);

        let bb_x_new = bb_x + bb_offset[0];
        let bb_y_new = bb_y + bb_offset[1];

        let i = 0;
        while (i < 4){
            let move_offset = move_offsets[i];
            let arr = Array.new(2);
            let arr[0] = bb_x_new + move_offset[0];
            let arr[1] = bb_y_new + move_offset[1];
            let new_coords_all[i] = arr;
            let i = i + 1;
        }
        return new_coords_all;
    }

    method void apply_move(Array new_coords_all, char direction){
        var int i, x_y_new;
        var Square sq;

        do hide();

        let i = 0;
        while (i < 4){
            let x_y_new = new_coords_all[i];
            let sq = Square.new(x_y_new[0], x_y_new[1]);
            let squares[i] = sq;
            let i = i + 1;
        }

        if (direction = direction_left){
            let bb_x = bb_x - 1;
        }
        else {
            if (direction = direction_right){
                let bb_x = bb_x + 1;
            }
            else {
                if (direction = direction_fall){
                    let bb_y = bb_y + 1;
                }
                else {
                    if (direction = direction_up){
                        let rotation = Utils.mod(rotation + 1, 4);
                    }
                    else {
                        let rotation = Utils.mod(rotation - 1, 4);
                    }
                }
            }
        }

        do show();
        return;
    }
}